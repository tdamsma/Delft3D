module test_longculverts
    use assertions_gtest
    use m_longculverts, only: convert1D2DLongCulverts
    implicit none

contains
    function to_c_string(string) result(res)
        use iso_c_binding, only: c_null_char
        implicit none
        character(len=*), intent(in) :: string
        character(len=:), allocatable :: res
        res = trim(string) // c_null_char
    end function to_c_string

    !> Sets up minimal network_data with a single rectangular netcell
    !! centered at (center_x, center_y) with given side length.
    !! This is useful for testing routines like incells that depend on network_data.
    subroutine generate_square_grid(bottom_left_x, bottom_left_y, side_length, rows, columns, array_size_margin)
        use precision, only: dp
        use network_data, only: xk, yk, zk, kc, nmk, numk, kn, nump, nump1d2d, netcell, tface, lc, numl, xzw, yzw, nod, rnod, LINK_2D
        use m_cell_geometry, only: xz, yz, ndx
        use m_alloc, only: realloc
        use m_dimens, only: kmax, lmax
        use m_set_nod_adm, only: setnodadm
        use gridoperations, only: findcells
        implicit none
        
        real(kind=dp), intent(in) :: bottom_left_x !< X-coordinate of cell center
        real(kind=dp), intent(in) :: bottom_left_y !< Y-coordinate of cell center
        real(kind=dp), intent(in) :: side_length !< Side length of square cell
        integer, intent(in) :: rows !< Number of rows
        integer, intent(in) :: columns !< Number of columns
        integer, optional, intent(in) :: array_size_margin
        integer :: array_size_margin_
        integer :: istat
        integer :: i, row, col, link_index, bottom_left_node_index, up_node_index, right_node_index, up_right_node_index

        array_size_margin_ = 0
        if (present(array_size_margin)) then
            array_size_margin_ = array_size_margin
        end if
        
        ! Set up 4 net nodes for a rectangular cell
        numk = (rows + 1) * (columns + 1)
        call realloc(xk, numk + array_size_margin_, stat=istat, fill=0.0_dp)
        call realloc(yk, numk + array_size_margin_, stat=istat, fill=0.0_dp)
        call realloc(zk, numk + array_size_margin_, stat=istat, fill=0.0_dp)
        call realloc(kc, numk + array_size_margin_, stat=istat, fill=1)
        call realloc(nmk, numk + array_size_margin_, stat=istat, fill=2)
        allocate(nod(numk + array_size_margin_))

        ! Place (rows+1)x(columns+1) grid nodes (the cell corners).
        do row = 0, rows
            do col = 0, columns
                xk(row * (columns + 1) + col + 1) = bottom_left_x + col * side_length
                yk(row * (columns + 1) + col + 1) = bottom_left_y + row * side_length
            end do
        end do
 
        ! Place links between nodes
        numl = 2 * rows * columns + rows + columns
        call realloc(kn, [3, numl + array_size_margin_], stat=istat, fill=0)
        call realloc(lc, numl + array_size_margin_, stat=istat, fill=1)
        link_index = 1
        do row = 0, rows - 1
            do col = 0, columns - 1
                bottom_left_node_index = row * (columns + 1) + col + 1
                right_node_index = bottom_left_node_index + 1
                up_node_index = bottom_left_node_index + columns + 1
                up_right_node_index = bottom_left_node_index + columns + 2

                if (row == 0) then
                    kn(:, link_index) = [bottom_left_node_index, right_node_index, LINK_2D]
                    link_index = link_index + 1
                end if
                kn(:, link_index) = [right_node_index, up_right_node_index, LINK_2D]
                kn(:, link_index + 1) = [up_right_node_index, up_node_index, LINK_2D]
                link_index = link_index + 2
                if (col == 0) then
                    kn(:, link_index) = [up_node_index, bottom_left_node_index, LINK_2D]
                    link_index = link_index + 1
                end if
            end do
        end do

        ! Initializes node, face and flow geometry stuff.
        call setnodadm(0)
        call findcells(0)
        
        kmax = 100
        lmax = 100
    end subroutine generate_square_grid
    
    !> Cleanup network_data arrays allocated by setup_single_rectangular_netcell
    subroutine cleanup_network_data()
        use network_data, only: xk, yk, zk, kc, nmk, numk, kn, nump, nump1d2d, netcell, tface, lc, numl, xzw, yzw, nod, rnod
        use m_cell_geometry, only: xz, yz, ndx
        implicit none
        
        integer :: i
        
        ! Deallocate node arrays
        if (allocated(xk)) then
            deallocate(xk)
        end if
        if (allocated(yk)) then
            deallocate(yk)
        end if
        if (allocated(zk)) then
            deallocate(zk)
        end if
        if (allocated(kc)) then
            deallocate(kc)
        end if
        if (allocated(nmk)) then
            deallocate(nmk)
        end if
        if (allocated(nod)) then
            ! Deallocate nod%lin arrays first
            do i = 1, size(nod)
                if (allocated(nod(i)%lin)) then
                    deallocate(nod(i)%lin)
                end if
            end do
            deallocate(nod)
        end if
        
        ! Deallocate link arrays
        if (allocated(kn)) then
            deallocate(kn)
        end if
        if (allocated(lc)) then
            deallocate(lc)
        end if
        if (allocated(rnod)) then
            deallocate(rnod)
        end if

        ! Deallocate cell arrays
        if (allocated(netcell)) then
            do i = 1, size(netcell)
                if (allocated(netcell(i)%nod)) then
                    deallocate(netcell(i)%nod)
                end if
                if (allocated(netcell(i)%lin)) then
                    deallocate(netcell(i)%lin)
                end if
            end do
            deallocate(netcell)
        end if
        
        if (allocated(xzw)) then
            deallocate(xzw)
        end if
        if (allocated(yzw)) then
            deallocate(yzw)
        end if
        
        ! Reset counters
        numk = 0
        numl = 0
        nump = 0
        nump1d2d = 0

        ! Reset flow administration
        if (allocated(xz)) then
            deallocate(xz)
        end if
        ndx = 0
    end subroutine cleanup_network_data

    !$f90tw TESTCODE(TEST, test_longculvert, test_convert1d2dlongculverts__single_four_point, test_convert1d2dlongculverts__single_four_point,
    subroutine test_convert1d2dlongculverts__single_four_point() bind(C)
        use precision, only: dp
        use network_data, only: numk, numl, kn
        use m_missing, only: dmiss
        use m_polygon, only: xpl, ypl, zpl, npl
        use m_longculverts, only: convert1D2DLongCulverts
        use m_longculverts_data, only: longculverts

        integer, parameter :: COORD_COUNT = 4
        real(kind=dp) :: x_coords(COORD_COUNT)
        real(kind=dp) :: y_coords(COORD_COUNT)
        real(kind=dp) :: z_coords(COORD_COUNT)
        integer :: i

        ! Arrange
        call generate_square_grid( &
            bottom_left_x=0.0_dp, bottom_left_y=0.0_dp, &
            rows=1, columns=2, side_length=10.0_dp, array_size_margin=16 &
        )

        x_coords = [5._dp, 9._dp, 11._dp, 15._dp]
        y_coords = [6._dp, 6._dp, 4._dp, 4._dp]
        z_coords = -1.0_dp

        ! Subroutine `longculvert_create_endpiont` requires these arrays in `m_polygon` to be allocated.
        xpl = x_coords
        ypl = y_coords
        zpl = z_coords
        npl = COORD_COUNT

        allocate(longculverts(1))
        allocate(longculverts(1)%netlinks(3))
        ! Act
        call convert1D2DLongCulverts(x_coords, y_coords, z_coords, COORD_COUNT)

        ! Assert
        call F90_ASSERT_DOUBLE_EQ(x_coords(1), 5._dp) ! First and last point snapped to cell centers.
        call F90_ASSERT_DOUBLE_EQ(y_coords(1), 5._dp)
        call F90_ASSERT_DOUBLE_EQ(x_coords(COORD_COUNT), 15._dp)
        call F90_ASSERT_DOUBLE_EQ(y_coords(COORD_COUNT), 5._dp)
        
        call F90_ASSERT_EQ(numk, 10) ! 6 Netnodes for the grid, 4 For the long culvert.
        call F90_ASSERT_EQ(numl, 10) ! 7 Netlinks for the grid, 3 For the long culvert.

        call F90_ASSERT_EQ(kn(3, longculverts(1)%netlinks(1)), 5, to_c_string("Expected first new link to be a 1D2D link."))
        call F90_ASSERT_EQ(kn(3, longculverts(1)%netlinks(2)), 1, to_c_string("Expected middle link to be a 1D link."))
        call F90_ASSERT_EQ(kn(3, longculverts(1)%netlinks(3)), 5, to_c_string("Expected last new link to be a 1D2D link."))
        
        call cleanup_network_data()
    end subroutine test_convert1d2dlongculverts__single_four_point
    !$f90tw )

    !$f90tw TESTCODE(TEST, test_longculvert, test_convert1d2dlongculverts__single_two_point, test_convert1d2dlongculverts__single_two_point,
    subroutine test_convert1d2dlongculverts__single_two_point() bind(C)
        use precision, only: dp
        use network_data, only: numk, numl, kn
        use m_missing, only: dmiss
        use m_polygon, only: xpl, ypl, zpl, npl
        use m_longculverts, only: convert1D2DLongCulverts
        use m_longculverts_data, only: longculverts

        implicit none

        integer, parameter :: COORD_COUNT = 2
        real(kind=dp) :: x_coords(COORD_COUNT)
        real(kind=dp) :: y_coords(COORD_COUNT)
        real(kind=dp) :: z_coords(COORD_COUNT)
        integer :: i

        npl = 0
        ! Arrange
        call generate_square_grid( &
            bottom_left_x=0.0_dp, bottom_left_y=0.0_dp, &
            rows=1, columns=2, side_length=10.0_dp, array_size_margin=16 &
        )

        x_coords = [5._dp, 15._dp]
        y_coords = [6._dp, 4._dp]
        z_coords = -1.0_dp

        ! `longculvert_create_endpiont` requires these arrays in `m_polygon` to be allocated.
        xpl = x_coords
        ypl = y_coords
        zpl = z_coords
        npl = COORD_COUNT
        if (allocated(longculverts)) then
            deallocate(longculverts)
        end if
        allocate(longculverts(1))
        allocate(longculverts(1)%netlinks(1))
        ! Act
        call convert1D2DLongCulverts(x_coords, y_coords, z_coords, COORD_COUNT)

        ! Assert
        call F90_ASSERT_DOUBLE_EQ(x_coords(1), 5._dp) ! First and last point snapped to cell centers.
        call F90_ASSERT_DOUBLE_EQ(y_coords(1), 5._dp)
        call F90_ASSERT_DOUBLE_EQ(x_coords(COORD_COUNT), 15._dp)
        call F90_ASSERT_DOUBLE_EQ(y_coords(COORD_COUNT), 5._dp)
        
        call F90_ASSERT_EQ(numk, 8) ! 6 Netnodes for the grid, 2 For the long culvert.
        call F90_ASSERT_EQ(numl, 8) ! 7 Netlinks for the grid, 1 For the long culvert.

        call F90_ASSERT_EQ(kn(3, longculverts(1)%netlinks(1)), 5, to_c_string("Expected first new link to be a 1D2D link."))
        
        call cleanup_network_data()
    end subroutine test_convert1d2dlongculverts__single_two_point
    !$f90tw )

    !$f90tw TESTCODE(TEST, test_longculvert, test_convert1d2dlongculverts__multiple_culverts, test_convert1d2dlongculverts__multiple_culverts,
    subroutine test_convert1d2dlongculverts__multiple_culverts() bind(C)
        use precision, only: dp
        use network_data, only: numk, numl, kn
        use m_missing, only: dmiss
        use m_polygon, only: xpl, ypl, zpl, npl
        use m_longculverts, only: convert1D2DLongCulverts
         use m_longculverts_data, only: longculverts
        use m_save_ugrid_state, only: meshgeom1d

        implicit none

        integer, parameter :: COORD_COUNT_LC1 = 4
        integer, parameter :: COORD_COUNT_LC2 = 2
        integer, parameter :: ARRAY_SIZE = COORD_COUNT_LC1 + COORD_COUNT_LC2 + 1
        real(kind=dp) :: x_coords(ARRAY_SIZE)
        real(kind=dp) :: y_coords(ARRAY_SIZE)
        real(kind=dp) :: z_coords(ARRAY_SIZE)
        integer :: i

        npl = 0
        ! Arrange
        ! 2 x 2 grid.
        call generate_square_grid( &
            bottom_left_x=0.0_dp, bottom_left_y=0.0_dp, &
            rows=2, columns=2, side_length=10.0_dp, array_size_margin=16 &
        )

        x_coords = [5._dp, 9._dp, 11._dp, 15._dp, dmiss, 5._dp, 15._dp]
        y_coords = [6._dp, 6._dp, 4._dp, 4._dp, dmiss, 16._dp, 14._dp]
        z_coords = -1.0_dp
        z_coords(5) = dmiss

        ! Subroutine `longculvert_create_endpiont` requires these arrays in `m_polygon` to be allocated.
        xpl = x_coords
        ypl = y_coords
        zpl = z_coords
        npl = ARRAY_SIZE

        !> ensure meshgeom1d state is disregarded
        meshgeom1d%numnode = -1 
        meshgeom1d%nnodes = -1

        if (allocated(longculverts)) then
            deallocate(longculverts)
        end if
        allocate(longculverts(2))
        allocate(longculverts(1)%netlinks(3))
        allocate(longculverts(2)%netlinks(1))

        ! Act
        call convert1D2DLongCulverts(x_coords, y_coords, z_coords, ARRAY_SIZE)

        ! Assert
        call F90_ASSERT_DOUBLE_EQ(x_coords(1), 5._dp) ! First and last point snapped to cell centers.
        call F90_ASSERT_DOUBLE_EQ(y_coords(1), 5._dp)
        call F90_ASSERT_DOUBLE_EQ(x_coords(COORD_COUNT_LC1), 15._dp)
        call F90_ASSERT_DOUBLE_EQ(y_coords(COORD_COUNT_LC1), 5._dp)
        call F90_ASSERT_DOUBLE_EQ(x_coords(COORD_COUNT_LC1 + 2), 5._dp) ! First and last point snapped to cell centers.
        call F90_ASSERT_DOUBLE_EQ(y_coords(COORD_COUNT_LC1 + 2), 15._dp)
        call F90_ASSERT_DOUBLE_EQ(x_coords(ARRAY_SIZE), 15._dp)
        call F90_ASSERT_DOUBLE_EQ(y_coords(ARRAY_SIZE), 15._dp)
        
        call F90_ASSERT_EQ(numk, 9 + 4 + 2) ! 9 Netnodes for the grid, 4 for LC1, 2 for LC2.
        call F90_ASSERT_EQ(numl, 12 + 3 + 1) ! 12 Netlinks for the grid, 3 for LC1, 1 for LC2.

        call cleanup_network_data()
    end subroutine test_convert1d2dlongculverts__multiple_culverts
    !$f90tw )

    !$f90tw TESTCODE(TEST, test_longculvert, test_longculvert_check_polyline__on_cell_center__shift_x_coord, test_longculvert_check_polyline__on_cell_center__shift_x_coord,
    subroutine test_longculvert_check_polyline__on_cell_center__shift_x_coord() bind(C)
        ! use precision, only: dp
        ! use m_cell_geometry, only: xz, yz
        ! use m_longculverts, only: longculvert_check_polyline
        ! implicit none

        ! real(kind=dp) :: x_coords(1)
        ! real(kind=dp) :: y_coords(1)
        ! real(kind=dp), parameter :: tolerance = 1e-8_dp

        ! ! Arrange
        ! call generate_square_grid(bottom_left_x=41.0_dp, bottom_left_y=41.0_dp, rows=1, columns=1, side_length=2.0_dp)
        ! x_coords(1) = 42_dp
        ! y_coords(1) = 42_dp

        ! ! Act
        ! call longculvert_check_polyline(1, y_coords, x_coords)
        
        ! ! Assert
        ! call f90_expect_near( &
        !     x_coords(1), 42.1_dp, tolerance, &
        !     to_c_string("The x coordinate has not been shifted away from the cell center by the expected amount.") &
        ! )

        ! call cleanup_network_data()
    end subroutine test_longculvert_check_polyline__on_cell_center__shift_x_coord
    !$f90tw )

    !$f90tw TESTCODE(TEST, test_longculvert, test_longculvert_check_polyline__off_cell_center, test_longculvert_check_polyline__off_cell_center,
    subroutine test_longculvert_check_polyline__off_cell_center() bind(C)
        ! use precision, only: dp
        ! use m_cell_geometry, only: xz, yz
        ! use m_longculverts, only: longculvert_check_polyline
        ! implicit none

        ! real(kind=dp) :: x_coords(1)
        ! real(kind=dp) :: y_coords(1)
        ! real(kind=dp), parameter :: tolerance = 1e-8_dp

        ! ! Arrange
        ! call generate_square_grid(bottom_left_x=41.0_dp, bottom_left_y=41.0_dp, rows=1, columns=1, side_length=2.0_dp)
        ! x_coords(1) = 42.0_dp
        ! y_coords(1) = 42.001_dp

        ! ! Act
        ! call longculvert_check_polyline(1, y_coords, x_coords)
        
        ! ! Assert
        ! call f90_expect_near( &
        !     x_coords(1), 42.0_dp, tolerance, &
        !     to_c_string("The x coordinate has been shifted away from the cell center.") &
        ! )

        ! call cleanup_network_data()
    end subroutine test_longculvert_check_polyline__off_cell_center
    !$f90tw )
end module test_longculverts