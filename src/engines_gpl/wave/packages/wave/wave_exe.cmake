# Gather source files
set(executable_files ${src_path}/wave_exe.f90) # Because the .dll and the .exe are defined in the same directory, retrieve the relevant files for the executable alone

# Define icon
set(icon_file resource/wl.ico)

# Define executable
set(executable_name wave_exe)
add_rc_object_library(${executable_name} "${rc_version_file};${icon_file}" "${version_include_dir};${wave_version_path}")
add_executable(${executable_name} ${executable_files})
target_link_libraries(${executable_name} PRIVATE ${executable_name}_rc)

target_link_libraries(${executable_name} PRIVATE
    wave_data
    delftio
    delftio_shm
    deltares_common
    deltares_common_c
    ec_module
    gridgeom
    wave_io
    io_netcdf
    wave_kernel
    wave_manager
    nefis
    netCDF::netcdff
    triangle::triangle
    swan
)

if(UNIX)
    target_link_libraries(${executable_name} PRIVATE esmfsm)
endif(UNIX)

include_directories(${mpi_module_path} ${version_include_dir})

if (WIN32)
    # Set linker properties
    message(STATUS "Setting linker properties on windows")
    target_link_directories(${executable_name}
                            PRIVATE
                            "${checkout_src_root}/third_party_open/pthreads/bin/x64"
                            "${mpi_library_path}")

    target_link_libraries(${executable_name} PRIVATE
                            "pthreadVC2.lib"
                            "${mpi_fortran_library}")

    # Set linker options
    message(STATUS "Setting target_link_options on windows")
    target_link_options(${executable_name} PRIVATE ${nologo_flag})
endif(WIN32)

if (UNIX)
    set_property(TARGET ${executable_name} PROPERTY LINKER_LANGUAGE Fortran)
endif(UNIX)

# Define how the files should be structured within Visual Studio
source_group(TREE ${CMAKE_CURRENT_SOURCE_DIR} FILES ${executable_files})
source_group(Resources FILES    ${rc_version_file}
                                ${icon_file})
set_target_properties (${executable_name} PROPERTIES FOLDER engines_gpl/wave)


# Change the name of the target library to wave.exe
set_target_properties (${executable_name} PROPERTIES OUTPUT_NAME wave_exe)
if (WIN32)
	set (userfilename "${CMAKE_BINARY_DIR}/template.vfproj.user")
	configure_file(
    ${userfilename}
    "${CMAKE_CURRENT_BINARY_DIR}/${executable_name}.vfproj.$ENV{USERNAME}.user"
    @ONLY
	)
endif(WIN32)

if (UNIX)
    install(PROGRAMS $<TARGET_FILE:${executable_name}> RENAME wave DESTINATION bin)
elseif (WIN32)
    install(PROGRAMS $<TARGET_FILE:${executable_name}> RENAME wave.exe DESTINATION bin)
endif()

install(PROGRAMS ${CMAKE_SOURCE_DIR}/../engines_gpl/wave/scripts/run_dwaves.${platform_extension}  DESTINATION bin)
if(UNIX AND NOT APPLE)
    install(PROGRAMS ${CMAKE_SOURCE_DIR}/../third_party_open/esmf/lnx64/scripts/ESMF_RegridWeightGen_in_Delft3D-WAVE.sh DESTINATION bin)
    find_program(ESMF_REGRIDWEIGHTGEN_EXECUTABLE ESMF_RegridWeightGen REQUIRED)
    install(PROGRAMS ${ESMF_REGRIDWEIGHTGEN_EXECUTABLE} DESTINATION bin)
elseif(APPLE)
    # ESMF has no macOS path: no Homebrew formula, and the repo only vendors
    # win64/lnx64 ESMF_RegridWeightGen binaries. WAVE's ESMF-based curvilinear
    # regridding is deferred on macOS until ESMF is ported/built natively;
    # skip installing the wrapper script and the (unfindable) executable
    # rather than fail configure on a REQUIRED find_program.
endif()
if(WIN32)
    install(PROGRAMS ${CMAKE_SOURCE_DIR}/../third_party_open/esmf/win64/scripts/ESMF_RegridWeightGen_in_Delft3D-WAVE.bat DESTINATION bin)
    install (DIRECTORY ${CMAKE_SOURCE_DIR}/../third_party_open/esmf/win64/bin/ DESTINATION bin
FILES_MATCHING
PATTERN "*.dll"
PATTERN "*.dll.a"
)
    install (PROGRAMS ${CMAKE_SOURCE_DIR}/../third_party_open/esmf/win64/bin/ESMF_RegridWeightGen.exe DESTINATION bin)
endif(WIN32)
